ALTER FUNCTION [dbo].[FN_KARTU_STOCK](
	@ITEM_ID INT,
	@TGLAWAL DATE,
	@TGLAKHIR DATE,
	@WAREHOUSE_ID INT	
)RETURNS @RESULT TABLE(	
	TRANSDATE DATE,
	ITEM_ID INT,
	QTYPCS FLOAT,
	HEADER_ID INT,
	HEADER_FLAG INT ,
	REFNO VARCHAR(30),
	NOTES VARCHAR(300)
) AS
BEGIN
	DECLARE @SALDOAKHIRDATE DATE = DATEADD(DAY,- 1, @TGLAWAL);

	if (@TGLAWAL <> DATEADD(yy, DATEDIFF(yy, 0, @TGLAWAL), 0)) 
	begin
		INSERT INTO @RESULT(TRANSDATE, ITEM_ID, QTYPCS, NOTES)
		SELECT @TGLAWAL, ITEM_ID, SUM(QTYPCS), 'Saldo Awal : ' + CONVERT(varchar, @TGLAWAL, 23)
		FROM FN_STOCK_BYITEM(@ITEM_ID, @SALDOAKHIRDATE)
		WHERE (WAREHOUSE_ID = @WAREHOUSE_ID OR @WAREHOUSE_ID = 0)
		GROUP BY ITEM_ID
	end

	--MUTASI
	INSERT INTO @RESULT(TRANSDATE, ITEM_ID, QTYPCS, HEADER_ID, HEADER_FLAG, REFNO, NOTES)
	SELECT A.TRANSDATE, A.ITEM_ID, SUM(A.QTY*A.KONVERSI), A.HEADER_ID, A.HEADER_FLAG, A.REFNO, B.TRANSNAME
	FROM TTRANSDETAIL A
	LEFT JOIN THEADERFLAG B ON A.HEADER_FLAG = B.HEADER_FLAG
	WHERE A.ITEM_ID = @ITEM_ID
	AND (A.WAREHOUSE_ID = @WAREHOUSE_ID OR @WAREHOUSE_ID = 0)
	AND A.TRANSDATE BETWEEN @TGLAWAL AND @TGLAKHIR
	GROUP BY A.TRANSDATE, A.ID, A.ITEM_ID, A.HEADER_ID, A.HEADER_FLAG, A.REFNO, B.TRANSNAME	
	ORDER BY A.TRANSDATE, A.ID

	RETURN

END
GO


